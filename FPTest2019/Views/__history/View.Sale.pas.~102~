unit View.Sale;

interface

uses
  Winapi.Windows,
  Winapi.Messages,
  System.SysUtils,
  System.Variants,
  System.Classes,
  System.Actions,
  Data.DB,
  Vcl.ExtCtrls,
  Vcl.Graphics,
  Vcl.Controls,
  Vcl.Forms,
  Vcl.Dialogs,
  Vcl.StdCtrls,
  Vcl.ActnList,
  Vcl.PlatformDefaultStyleActnCtrls,
  Vcl.ActnMan,
  Vcl.Grids,
  Vcl.DBGrids,
  Vcl.Mask,
  Vcl.DBCtrls,
  Vcl.ComCtrls,
  Interfaces.Model.Notification,
  Interfaces.Model.Pattern.Observer,
  Interfaces.ViewModel.Sale,
  DataModule.Sale;

type
  TViewSale = class(TForm, IObserver, IObservable)
    ActionManager: TActionManager;
      ActionExit: TAction;
      ActionVoucherPayment: TAction;
      ActionCardPayment: TAction;
      ActionPayment: TAction;
      ActionRemoveItem: TAction;
      ActionCheckItem: TAction;
      ActionSaveAndNew: TAction;
      ActionDiscard: TAction;
      ActionDuplicateReceipt: TAction;

    TimerClose: TTimer;

    PanelButtons: TPanel;
      ButtonExit: TButton;
      ButtonVoucherPayment: TButton;
      ButtonCardPayment: TButton;
      ButtonPayment: TButton;
      ButtonRemoveItem: TButton;
      ButtonCheckItem: TButton;
      ButtonSaveAndNew: TButton;
      ButtonDiscard: TButton;
      ButtonDuplicateReceipt: TButton;

    PanelShow: TPanel;
      EditShow: TDBEdit;

    PanelGrid: TPanel;
      Grid: TDBGrid;

    PanelPayment: TPanel;
      LabelCashPayment: TLabel;
      EditCashPayment: TDBEdit;
      LabelVoucherPayment: TLabel;
      EditVoucherPayment: TDBEdit;
      LabelCardPayment: TLabel;
      EditCardPayment: TDBEdit;
      LabelReturned: TLabel;
      EditReturned: TDBEdit;
      LabelTotal: TLabel;
      EditTotal: TDBEdit;

    LabelMinVIP: TLabel;

    StatusBar: TStatusBar;

  {$REGION 'Published Methods'}
    procedure FormCreate(Sender: TObject);
    procedure FormClose(Sender: TObject; var Action: TCloseAction);

    procedure ActionExitExecute(Sender: TObject);               // ESC
    procedure ActionVoucherPaymentExecute(Sender: TObject);     // F3
    procedure ActionCardPaymentExecute(Sender: TObject);        // F4
    procedure ActionPaymentExecute(Sender: TObject);            // F6
    procedure ActionRemoveItemExecute(Sender: TObject);         // F8
    procedure ActionCheckItemExecute(Sender: TObject);          // F9
    procedure ActionSaveAndNewExecute(Sender: TObject);         // F10
    procedure ActionDiscardExecute(Sender: TObject);            // Ctrl+F11

    procedure EditShowEnter(Sender: TObject);
    procedure GridDrawColumnCell(Sender: TObject; const Rect: TRect; DataCol: Integer; Column: TColumn; State: TGridDrawState);
    procedure GridEnter(Sender: TObject);
    procedure GridKeyPress(Sender: TObject; var Key: Char);
    procedure EditTotalEnter(Sender: TObject);
    procedure EditCashPaymentEnter(Sender: TObject);
    procedure EditCashPaymentKeyPress(Sender: TObject; var Key: Char);
    procedure EditVoucherPaymentEnter(Sender: TObject);
    procedure EditVoucherPaymentKeyPress(Sender: TObject; var Key: Char);
    procedure EditCardPaymentEnter(Sender: TObject);
    procedure EditCardPaymentKeyPress(Sender: TObject; var Key: Char);
    procedure EditCardPaymentExit(Sender: TObject);
    procedure EditReturnedEnter(Sender: TObject);
    procedure ActionDuplicateReceiptExecute(Sender: TObject);
  {$ENDREGION}

  {$REGION 'Private Methods'}
  private
    procedure ProcessNotification(const AModelNotification: IModelNotification);

    procedure SetupGUI;
    procedure UpdateGUI;
    procedure DisableActions;
    procedure EnableActions;
  {$ENDREGION}

  {$REGION 'Private Fields'}
  private
    FViewModel: IViewModelSale;
    FObserver: IObserver;
    FObservable: IObservable;
  {$ENDREGION}

  {$REGION 'Private Properties Getters/Setters'}
  private

  {$ENDREGION}

  {$REGION 'Private Properties'}
  private

  {$ENDREGION}

  {$REGION 'Interfaced Properties Getters/Setters'}
  public
    function GetObserver: IObserver;
    function GetObservable: IObservable;
    function GetViewModel: IViewModelSale;
  {$ENDREGION}

  {$REGION 'Interfaced Properties'}
  public
    property Observer: IObserver read FObserver implements IObserver;
    property Observable: IObservable read FObservable implements IObservable;
    property ViewModel: IViewModelSale read GetViewModel;
  {$ENDREGION}

  {$REGION 'Interfaced Methods'}
  public

  {$ENDREGION}

  {$REGION 'Constructors/Destructors'}
  public

  {$ENDREGION}
  end;

  procedure ShowViewSale;

implementation

{$R *.dfm}

uses
  System.UITypes,
  Interfaces.Enums,
  Interfaces.GUIRecords,
  Model.Pattern.Observer.Observer,
  Model.Pattern.Observer.Observable,
  ViewModel.Sale;

{$REGION 'Published Methods'}

procedure TViewSale.FormCreate(Sender: TObject);
begin
  FObserver := CreateObserverClass;
  FObserver.SetUpdateObserverMethod(ProcessNotification);

  FObservable := CreateObservableClass;

  FViewModel := CreateViewModelSale;
  FViewModel.Observable.Subscribe(FObserver);

  ViewModel.SetupSale;
end;

procedure TViewSale.FormClose(Sender: TObject; var Action: TCloseAction);
begin
  ViewModel.TeardownSale;

  FViewModel.Observable.UnSubscribe(FObserver);

  Action := caFree;
end;

// Actions

procedure TViewSale.ActionExitExecute(Sender: TObject);
begin
  ViewModel.ActionExitExecute;
end;

procedure TViewSale.ActionVoucherPaymentExecute(Sender: TObject);
begin
  ViewModel.ActionVoucherPaymentExecute;
end;

procedure TViewSale.ActionCardPaymentExecute(Sender: TObject);
begin
  ViewModel.ActionCardPaymentExecute;
end;

procedure TViewSale.ActionPaymentExecute(Sender: TObject);
begin
  ViewModel.ActionPaymentExecute;
end;

procedure TViewSale.ActionRemoveItemExecute(Sender: TObject);
begin
  ViewModel.ActionRemoveItemExecute;
end;

procedure TViewSale.ActionCheckItemExecute(Sender: TObject);
begin
  ViewModel.ActionCheckItemExecute;
end;

procedure TViewSale.ActionSaveAndNewExecute(Sender: TObject);
begin
  ViewModel.ActionSaveAndNewExecute;
end;

procedure TViewSale.ActionDiscardExecute(Sender: TObject);
begin
  ViewModel.ActionDiscardExecute;
end;


procedure TViewSale.ActionDuplicateReceiptExecute(Sender: TObject);
begin
  ViewModel.ActionDuplicateReceiptExecute;
end;

// Edit Controls

procedure TViewSale.EditShowEnter(Sender: TObject);
begin
  ViewModel.EditShowEnter;
end;

procedure TViewSale.GridDrawColumnCell(Sender: TObject; const Rect: TRect; DataCol: Integer; Column: TColumn; State: TGridDrawState);
begin
  with (Sender as TDBGrid) do begin
    if (DataSource.DataSet.FieldByName('IsCancelled').AsString = 'да') and (Column.FieldName = 'ItemName') then begin
      Canvas.Font.Style := [fsBold, fsStrikeOut];
    end;
    DefaultDrawColumnCell(Rect, DataCol, Column, State);
  end;
end;

procedure TViewSale.GridEnter(Sender: TObject);
begin
  ViewModel.GridEnter;
end;

procedure TViewSale.GridKeyPress(Sender: TObject; var Key: Char);
begin
  ViewModel.GridKeyPress(Key);
end;

procedure TViewSale.EditTotalEnter(Sender: TObject);
begin
  ViewModel.EditTotalEnter;
end;

procedure TViewSale.EditCashPaymentEnter(Sender: TObject);
begin
  ViewModel.EditCashPaymentEnter;
end;

procedure TViewSale.EditCashPaymentKeyPress(Sender: TObject; var Key: Char);
begin
  ViewModel.EditCashPaymentKeyPress(Key, EditCashPayment.Text);
end;

procedure TViewSale.EditVoucherPaymentEnter(Sender: TObject);
begin
  ViewModel.EditVoucherPaymentEnter;
end;

procedure TViewSale.EditVoucherPaymentKeyPress(Sender: TObject; var Key: Char);
begin
  ViewModel.EditVoucherPaymentKeyPress(Key, EditVoucherPayment.Text);
end;

procedure TViewSale.EditCardPaymentEnter(Sender: TObject);
begin
  ViewModel.EditCardPaymentEnter;
end;

procedure TViewSale.EditCardPaymentKeyPress(Sender: TObject; var Key: Char);
begin
  ViewModel.EditCardPaymentKeyPress(Key, EditCardPayment.Text);
end;

procedure TViewSale.EditCardPaymentExit(Sender: TObject);
begin
  ViewModel.EditCardPaymentExit(EditCardPayment.Text);
end;

procedure TViewSale.EditReturnedEnter(Sender: TObject);
begin
  ViewModel.EditReturnedEnter;
end;

{$ENDREGION}


{$REGION 'Private Methods'}

procedure TViewSale.ProcessNotification(const AModelNotification: IModelNotification);
begin
  if actSetupGUI in AModelNotification.Actions then begin
    SetupGUI;
  end;
  if actUpdateGUI in AModelNotification.Actions then begin
    UpdateGUI;
  end;
  if actSaleDisableActions in AModelNotification.Actions then begin
    DisableActions;
  end;
  if actSaleActiveControlGrid in AModelNotification.Actions then begin
    ActiveControl := Grid;
  end;
  if actSaleActiveControlEditCashPayment in AModelNotification.Actions then begin
    ActiveControl := EditCashPayment;
    EditCashPayment.SelectAll;
  end;
  if actSaleActiveControlEditVoucherPayment in AModelNotification.Actions then begin
    ActiveControl := EditVoucherPayment;
    EditVoucherPayment.SelectAll;
  end;
  if actSaleActiveControlEditCardPayment in AModelNotification.Actions then begin
    ActiveControl := EditCardPayment;
    EditCardPayment.SelectAll;
  end;
  if actSaleActiveControlEditReturned in AModelNotification.Actions then begin
    ActiveControl := EditReturned;
  end;
  if actSaleActiveControlToggleEditCashPayment in AModelNotification.Actions then begin
    if ActiveControl <> Grid then begin
      ActiveControl := Grid;
    end else begin
      ActiveControl := EditCashPayment;
    end;
  end;
  if actSaleEnableActions in AModelNotification.Actions then begin
    EnableActions;
  end;
  if actCloseForm in AModelNotification.Actions then begin
    Close;
  end;
end;

procedure TViewSale.SetupGUI;
var
  LViewSaleGUISetupRecord: TViewSaleGUISetupRecord;
begin
  LViewSaleGUISetupRecord := ViewModel.GetGUISetupRecord(Screen.Width - 16, Screen.Height - 16);

  Color := LViewSaleGUISetupRecord.Color;
  Top := LViewSaleGUISetupRecord.Top;
  Left := LViewSaleGUISetupRecord.Left;
  Width := LViewSaleGUISetupRecord.Width;
  Height := LViewSaleGUISetupRecord.Height;

  // Panel Buttons
  ButtonExit.Left := LViewSaleGUISetupRecord.ButtonExitLeft;
  ButtonExit.Width := LViewSaleGUISetupRecord.ButtonExitWidth;

  ButtonVoucherPayment.Left := LViewSaleGUISetupRecord.ButtonVoucherPaymentLeft;
  ButtonVoucherPayment.Width := LViewSaleGUISetupRecord.ButtonVoucherPaymentWidth;

  ButtonCardPayment.Left := LViewSaleGUISetupRecord.ButtonCardPaymentLeft;
  ButtonCardPayment.Width := LViewSaleGUISetupRecord.ButtonCardPaymentWidth;

  ButtonPayment.Left := LViewSaleGUISetupRecord.ButtonPaymentLeft;
  ButtonPayment.Width := LViewSaleGUISetupRecord.ButtonPaymentWidth;

  ButtonRemoveItem.Left := LViewSaleGUISetupRecord.ButtonRemoveItemLeft;
  ButtonRemoveItem.Width := LViewSaleGUISetupRecord.ButtonRemoveItemWidth;

  ButtonCheckItem.Left := LViewSaleGUISetupRecord.ButtonCheckItemLeft;
  ButtonCheckItem.Width := LViewSaleGUISetupRecord.ButtonCheckItemWidth;

  ButtonSaveAndNew.Left := LViewSaleGUISetupRecord.ButtonSaveAndNewLeft;
  ButtonSaveAndNew.Width := LViewSaleGUISetupRecord.ButtonSaveAndNewWidth;

  ButtonDiscard.Left := LViewSaleGUISetupRecord.ButtonDiscardLeft;
  ButtonDiscard.Width := LViewSaleGUISetupRecord.ButtonDiscardWidth;

  // Panel Show
  EditShow.Left := LViewSaleGUISetupRecord.EditShowLeft;
  EditShow.Width := LViewSaleGUISetupRecord.EditShowWidth;

  // Panel Grid
  PanelGrid.Width := LViewSaleGUISetupRecord.PanelGridWidth;
  PanelGrid.Height := LViewSaleGUISetupRecord.PanelGridHeight;
  Grid.Top := LViewSaleGUISetupRecord.GridTop;
  Grid.Left := LViewSaleGUISetupRecord.GridLeft;
  Grid.Height := LViewSaleGUISetupRecord.GridHeight;
  Grid.Width := LViewSaleGUISetupRecord.GridWidth;
  Grid.Columns[0].Width := LViewSaleGUISetupRecord.GridColumns0Width;
  Grid.Columns[1].Width := LViewSaleGUISetupRecord.GridColumns1Width;
  Grid.Columns[2].Width := LViewSaleGUISetupRecord.GridColumns2Width;
  Grid.Columns[3].Width := LViewSaleGUISetupRecord.GridColumns3Width;
  Grid.Columns[4].Width := LViewSaleGUISetupRecord.GridColumns4Width;
  Grid.Columns[5].Width := LViewSaleGUISetupRecord.GridColumns5Width;

  // Panel Payment
  LabelTotal.Left := LViewSaleGUISetupRecord.LabelTotalLeft;
  EditTotal.Left := LViewSaleGUISetupRecord.EditTotalLeft;
  LabelCashPayment.Left := LViewSaleGUISetupRecord.LabelCashPaymentLeft;
  EditCashPayment.Left := LViewSaleGUISetupRecord.EditCashPaymentLeft;
  LabelVoucherPayment.Left := LViewSaleGUISetupRecord.LabelVoucherPaymentLeft;
  EditVoucherPayment.Left := LViewSaleGUISetupRecord.EditVoucherPaymentLeft;
  LabelCardPayment.Left := LViewSaleGUISetupRecord.LabelCardPaymentLeft;
  EditCardPayment.Left := LViewSaleGUISetupRecord.EditCardPaymentLeft;
  LabelReturned.Left := LViewSaleGUISetupRecord.LabelReturnedLeft;
  EditReturned.Left := LViewSaleGUISetupRecord.EditReturnedLeft;

  LabelMinVIP.Caption := LViewSaleGUISetupRecord.LabelMinVIPCaption;

  Self.Update;
end;

procedure TViewSale.UpdateGUI;
var
  LViewSaleGUIRecord: TViewSaleGUIRecord;
begin
  LViewSaleGUIRecord := ViewModel.GetGUIRecord;
  Caption := LViewSaleGUIRecord.Caption;
  Color := LViewSaleGUIRecord.Color;
  Grid.Color := LViewSaleGUIRecord.GridColor;
  EditReturned.Font.Color := LViewSaleGUIRecord.EditReturnedFontColor;
  EditReturned.Brush.Color := LViewSaleGUIRecord.EditReturnedBrushColor;
  Self.Update;
end;

procedure TViewSale.DisableActions;
begin
  ActionExit.Enabled := False;
  ActionVoucherPayment.Enabled := False;
  ActionCardPayment.Enabled := False;
  ActionPayment.Enabled := False;
  ActionRemoveItem.Enabled := False;
  ActionCheckItem.Enabled := False;
  ActionSaveAndNew.Enabled := False;
  ActionDiscard.Enabled := False;

  LabelMinVIP.Visible := False;
end;

procedure TViewSale.EnableActions;
var
  LViewSaleGUIActionsRecord: TViewSaleGUIActionsRecord;
begin
  LViewSaleGUIActionsRecord := ViewModel.GetGUIActionsRecord;

  ActionExit.Enabled := LViewSaleGUIActionsRecord.ActionExitEnabled;
  ActionVoucherPayment.Enabled := LViewSaleGUIActionsRecord.ActionVoucherPaymentEnabled;
  ActionCardPayment.Enabled := LViewSaleGUIActionsRecord.ActionCardPaymentEnabled;
  ActionPayment.Enabled := LViewSaleGUIActionsRecord.ActionPaymentEnabled;
  ActionRemoveItem.Enabled := LViewSaleGUIActionsRecord.ActionRemoveItemEnabled;
  ActionCheckItem.Enabled := LViewSaleGUIActionsRecord.ActionCheckItemEnabled;
  ActionSaveAndNew.Enabled := LViewSaleGUIActionsRecord.ActionSaveAndNewEnabled;
  ActionDiscard.Enabled := LViewSaleGUIActionsRecord.ActionDiscardEnabled;
  LabelMinVIP.Visible := LViewSaleGUIActionsRecord.LabelMinVIPVisible;

  UpdateGUI;

  Self.Update;
end;

{$ENDREGION}


{$REGION 'Private Properties Getters/Setters'}

{$ENDREGION}


{$REGION 'Interfaced Properties Getters/Setters'}

function TViewSale.GetObserver: IObserver;
begin
  Result := FObserver;
end;

function TViewSale.GetObservable: IObservable;
begin
  Result := FObservable;
end;

function TViewSale.GetViewModel: IViewModelSale;
begin
  Result := FViewModel;
end;

{$ENDREGION}


{$REGION 'Interfaced Methods'}

{$ENDREGION}


{$REGION 'Constructors/Destructors'}

{$ENDREGION}

procedure ShowViewSale;
var
  LViewSale: TViewSale;
begin
  LViewSale := TViewSale.Create(nil);
  LViewSale.ShowModal;
end;

end.
