unit ViewModel.CheckItem;

interface

uses
  Interfaces.ViewModel.CheckItem;

  function CreateViewModelCheckItem: IViewModelCheckItem;

implementation

uses
  Winapi.Windows,
  System.SysUtils,
  System.Classes,
  Vcl.ExtCtrls,
  Globals,
  Interfaces.Enums,
  Interfaces.GUIRecords,
  Interfaces.Model.Pattern.Observer,
  Interfaces.Model.Notification,
  Model.Pattern.Observer.Observer,
  Model.Pattern.Observer.Observable,
  Model.Notification,

  Interfaces.Model.CheckItem,
  DataModule.Items,
  Model.CheckItem;

type
  TViewModelCheckItem = class(TInterfacedObject, IViewModelCheckItem, IObservable)

  {$REGION 'Private Methods'}
  private
    procedure OnTimer(Sender: TObject);
    procedure SendNotification(const AInterfaceActions: TInterfaceActions);
  {$ENDREGION}

  {$REGION 'Private Fields'}
  private
    FModel: IModelCheckItem;
    FObservable: IObservable;
    FTimer: TTimer;
    FIsQuick: Boolean;
    FIsChanged: Boolean;
  {$ENDREGION}

  {$REGION 'Private Properties Getters/Setters'}
  private

  {$ENDREGION}

  {$REGION 'Private Properties'}
  private

  {$ENDREGION}

  {$REGION 'Interfaced Properties Getters/Setters'}
  public
    function GetObservable: IObservable;
    function GetModel: IModelCheckItem;
    procedure SetModel(const Value: IModelCheckItem);
  {$ENDREGION}

  {$REGION 'Interfaced Properties'}
  public
    property Observable: IObservable read GetObservable implements IObservable;
    property Model: IModelCheckItem read GetModel write SetModel;
  {$ENDREGION}

  {$REGION 'Interfaced Methods'}
  public
    function GetGUIRecord(const AWidth, AHeight: Integer): TViewCheckItemGUIRecord;
    procedure GridKeyDown(var Key: Word; Shift: TShiftState);
    procedure GridKeyPressed(Key: Char);
  {$ENDREGION}

  {$REGION 'Constructors/Destructors'}
  public
    constructor Create;
    destructor Destroy; override;
  {$ENDREGION}
  end;

function CreateViewModelCheckItem: IViewModelCheckItem;
begin
  Result := TViewModelCheckItem.Create;
end;

{ TViewModelCheckItem }

{$REGION 'Private Methods'}

procedure TViewModelCheckItem.OnTimer(Sender: TObject);
begin
  FIsQuick := False;
  If FIsChanged then begin
    DataModuleItems.Refilter;
    FIsChanged := False;
  end;
  FTimer.Enabled := False;
end;

procedure TViewModelCheckItem.SendNotification(const AInterfaceActions: TInterfaceActions);
var
  LModelNotification: IModelNotification;
begin
  if Assigned(FObservable) then begin
    LModelNotification := CreateNotificationClass;
    LModelNotification.Actions := AInterfaceActions;
    FObservable.NotifyObservers(LModelNotification);
  end;
end;

{$ENDREGION}


{$REGION 'Private Properties Getters/Setters'}

{$ENDREGION}


{$REGION 'Interfaced Properties Getters/Setters'}

function TViewModelCheckItem.GetObservable: IObservable;
begin
  Result := FObservable;
end;

function TViewModelCheckItem.GetModel: IModelCheckItem;
begin
  Result := FModel;
end;

procedure TViewModelCheckItem.SetModel(const Value: IModelCheckItem);
begin
  FModel := Value;
end;

{$ENDREGION}


{$REGION 'Interfaced Methods'}

function TViewModelCheckItem.GetGUIRecord(const AWidth, AHeight: Integer): TViewCheckItemGUIRecord;
begin
  Result.Top := AHeight - 300;
  Result.Left := 8;
  Result.EditText := DataModuleItems.KeyWord;
end;

procedure TViewModelCheckItem.GridKeyDown(var Key: Word; Shift: TShiftState);
begin
  FIsQuick := True;
  FTimer.Enabled := False;
  FTimer.Enabled := True;
  case Key of
    VK_BACK : begin
      DataModuleItems.KeyWord := Copy(DataModuleItems.KeyWord, 1, Length(DataModuleItems.KeyWord)-1);
      FIsChanged := True;
      SendNotification([actUpdateGUI]);
    end;
    VK_ESCAPE: begin
      DataModuleItems.ClearSelected;
      SendNotification([actCloseForm]);
    end;
    VK_RETURN : begin
      If FIsQuick then begin
        DataModuleItems.Refilter;
      end;
      DataModuleItems.Select;
      SendNotification([actCloseForm]);
    end;
  else
    FIsChanged := False;
  end;
end;

procedure TViewModelCheckItem.GridKeyPressed(Key: Char);
begin
  if CharInSet(Key,[' '..'~', 'À'..'ÿ']) then begin
    DataModuleItems.KeyWord := DataModuleItems.KeyWord + Key;
    FIsChanged := True;
    SendNotification([actUpdateGUI]);
  end;
end;

{$ENDREGION}


{$REGION 'Constructors/Destructors'}

constructor TViewModelCheckItem.Create;
begin
  inherited;
  FObservable := CreateObservableClass;
  FModel := CreateModelCheckItem;
  FTimer := TTimer.Create(nil);
  FTimer.Enabled := False;
  FTimer.Interval := 200;
  FTimer.OnTimer := OnTimer;
end;

destructor TViewModelCheckItem.Destroy;
begin
  FTimer.Free;
  inherited;
end;

{$ENDREGION}

end.
